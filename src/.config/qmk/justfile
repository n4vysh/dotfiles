root_dir := justfile_directory()
keyboard := "zsa/voyager"
keymap := "custom"
origin_dir := root_dir + "/firmware/keyboards/" + keyboard + "/keymaps/" + keymap

flash:
    qmk flash

_stow *opts:
    stow {{ opts }} -v -d {{ root_dir }}/keyboards/{{ keyboard }}/keymap -t {{ origin_dir }} .

link: _stow

unlink: (_stow "-D")

dry-run-link: (_stow "-n")

dry-run-unlink: (_stow "-n" "-D")

init: init-submodule create-dummy-file link setup

init-submodule:
    git submodule update \
        --init \
        --recursive \
        --recommend-shallow \
        --depth=1 \
        --jobs=$(nproc) \
        firmware/

update-submodule:
    git submodule update \
        --remote \
        --recursive \
        --recommend-shallow \
        --depth=1 \
        --jobs=$(nproc) \
        firmware/
    make -C firmware git-submodule

create-dummy-file:
    mkdir -p {{ origin_dir }}
    touch {{ origin_dir }}/readme.md # resolve lint error

# NOTE: not work DFU_ARGS and DFU_SUFFIX_ARGS in rules.mk
update-udev-rules:
    #!/bin/bash

    if ! grep -q '# ZSA Voyager' /etc/udev/rules.d/50-qmk.rules; then
        cat <<EOF | sudo tee -a /etc/udev/rules.d/50-qmk.rules >/dev/null

    # ZSA Voyager
    SUBSYSTEMS=="usb", ATTRS{idVendor}=="3297", ATTRS{idProduct}=="0791", TAG+="uaccess"
    EOF
        sudo udevadm control --reload-rules
        sudo udevadm trigger
    fi

setup:
    sudo cp {{ root_dir }}/firmware/util/udev/50-qmk.rules /etc/udev/rules.d/
    just update-udev-rules
    qmk setup

compile:
    qmk compile

lint:
    qmk lint --strict

doctor:
    qmk doctor

info:
    qmk info

clean:
    qmk clean

fmt:
    qmk format-c {{ root_dir }}/keyboards/{{ keyboard }}/keymap/{config.h,keymap.c}
